plugins {
    id 'java-library'
    id 'com.google.protobuf'
}

sourceSets {
    main {
        java.srcDirs = [
            'src/main/java'
            , 'build/generated-sources'
            , 'build/generated/source/proto/main/grpc'
            , 'build/generated/source/proto/main/java']
    }
}

dependencies {
    implementation project(':common:common-core')
    implementation libs.slf4j.api

    api libs.grpc.protobuf
    api libs.grpc.stub
    api libs.grpc.netty.shaded
    api libs.protobuf.java.util
    api "jakarta.annotation:jakarta.annotation-api:1.3.5"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${libs.versions.protoc.get()}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${libs.versions.grpc.get()}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

compileJava.dependsOn generateProto
