dependencies {
    intTestImplementation project(':common:common-test')
    intTestImplementation project(":central-server:admin-service:api-client")
    intTestImplementation(testFixtures(project(":common:common-management-request")))
    intTestImplementation(libs.feign.hc5)

    intTestImplementation(libs.test.automation.core)
    intTestImplementation(libs.test.automation.assert)
    intTestImplementation(libs.test.automation.containers) {
        exclude(group: "org.bouncycastle", module: "bcpkix-jdk18on")
        exclude(group: "org.bouncycastle", module: "bcprov-jdk18on")
    }
    intTestImplementation(libs.test.automation.feign)
    intTestImplementation(libs.test.automation.allure)
}

test {
    useJUnitPlatform()
}

tasks.register('intTest', Test) {
    useJUnitPlatform()

    setDescription("Runs integration tests.")
    group = 'verification'

    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath

    def intTestArgs = []
    if (project.hasProperty('intTestTags')) {
        intTestArgs += "-Dtest-automation.cucumber.filter.tags=" + project.getProperty('intTestTags')
    }
    if (project.hasProperty('intTestProfilesInclude')) {
        intTestArgs += "-Dspring.profiles.include=" + project.getProperty('intTestProfilesInclude')
    }

    jvmArgs intTestArgs

    testLogging {
        showStackTraces(true)
        showExceptions(true)
        showCauses(true)
        showStandardStreams(true)
    }

    shouldRunAfter test
}

tasks.named('check') {
    dependsOn tasks.named('intTest')
}
