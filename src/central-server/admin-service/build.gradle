subprojects {

    def standaloneModules = ["ui", "ui-system-test", "int-test"]
    if (!standaloneModules.contains(it.name)) {
        apply {
            plugin 'io.spring.dependency-management'
            plugin 'org.springframework.boot'
            plugin 'com.github.hierynomus.license'
        }

        tasks.withType(JavaCompile).configureEach {
            options.encoding = 'UTF-8'
            options.compilerArgs += [
                "-Amapstruct.unmappedTargetPolicy=ERROR"
            ]
        }

        dependencies {
            annotationProcessor("org.mapstruct:mapstruct-processor:${libs.versions.mapstruct.get()}")
            annotationProcessor("org.projectlombok:lombok-mapstruct-binding:0.2.0")

            implementation platform(libs.springCloud.dependencies)
            implementation "org.mapstruct:mapstruct:${libs.versions.mapstruct.get()}"

            testAnnotationProcessor "org.mapstruct:mapstruct-processor:${libs.versions.mapstruct.get()}"
        }

        bootJar {
            enabled = false
        }
    }

}

tasks.withType(Jar).configureEach {
    enabled = false
}
